module Model where

import DA.Set (Set)
import qualified DA.Set as Set
import DA.Foldable (forA_)

template JobOfferPublicState
  with
    jobId: Int
    hrManager: Party
    public: Party
    -- XXX: here we keep the contract ids instead of the contract keys because
    -- we want candidates to remain private for the public observer.
    -- this attribute is meant for public observers to know how many candidates
    -- are in pending to accept/reject for the job offer.
    jobApplicationCids: Set (ContractId JobApplication)
  where
    signatory hrManager
    observer public

    ensure hrManager /= public
    
    key (hrManager, jobId): (Party, Int)
    maintainer key._1

    choice RegisterJobApplication: ContractId JobOfferPublicState
      with
        jobApplicationCid: ContractId JobApplication
      controller hrManager
      do
        fetch jobApplicationCid
        create this with
          jobApplicationCids = Set.insert jobApplicationCid jobApplicationCids

    choice RemoveJobApplication: ContractId JobOfferPublicState
      with
        jobApplicationCid: ContractId JobApplication
      controller hrManager
      do
        fetch jobApplicationCid
        create this with
          jobApplicationCids = Set.delete jobApplicationCid jobApplicationCids

    nonconsuming choice Terminate: ()
      with
        jobApplicationWinnerCid: ContractId JobApplication
      controller hrManager
      do
        fetch jobApplicationWinnerCid
        assertMsg "winner not exists" $ (Set.member jobApplicationWinnerCid jobApplicationCids)
        forA_ (Set.filter (/= jobApplicationWinnerCid) jobApplicationCids) $
          \jobApplicationCid -> exercise jobApplicationCid Reject

template JobOffer
  with
    jobId: Int
    hrManager: Party
    public: Party
  where
    signatory hrManager
    observer public

    ensure hrManager /= public

    key (hrManager, jobId): (Party, Int)
    maintainer key._1
  
    nonconsuming choice NewJobApplication: ContractId JobApplication
      with
        candidate: Party
        summary: Text
      controller candidate
      do
        assertMsg "hr manager cannot apply to a job offer" (hrManager /= candidate)
        jobApplicationCid <- create $ JobApplication with jobId; candidate; hrManager; summary
        exerciseByKey @JobOfferPublicState (hrManager, jobId) RegisterJobApplication with jobApplicationCid
        pure jobApplicationCid

    nonconsuming choice RejectJobApplication: ()  
      with
        jobApplicationCid: ContractId JobApplication
      controller hrManager
      do 
        exerciseByKey @JobOfferPublicState (hrManager, jobId) RemoveJobApplication with jobApplicationCid
        pure ()

    nonconsuming choice TerminateJobOffer: ()
      with
        jobApplicationWinnerCid: ContractId JobApplication
      controller hrManager
      do
        exerciseByKey @JobOfferPublicState (hrManager, jobId) Terminate with jobApplicationWinnerCid
        exerciseByKey @JobOfferPublicState (hrManager, jobId) Archive
        archive self
    
template JobApplication
  with
    jobId: Int
    candidate: Party
    hrManager: Party
    summary: Text
  where
    signatory candidate, hrManager

    ensure candidate /= hrManager

    key (candidate, hrManager, jobId): (Party, Party, Int)
    maintainer key._1

    nonconsuming choice Accept: (ContractId JobContract)
      controller hrManager
      do
        jobContractCid <- create $ JobContract with employee=candidate; hrManager
        exerciseByKey @JobOffer (hrManager, jobId) TerminateJobOffer with jobApplicationWinnerCid=self
        archive self
        pure jobContractCid
    
    nonconsuming choice Reject: ()
      controller hrManager
      do
        exerciseByKey @JobOffer (hrManager, jobId) RejectJobApplication with jobApplicationCid=self
        archive self

    nonconsuming choice Decline: ()
      controller candidate
      do
        exerciseByKey @JobOffer (hrManager, jobId) RejectJobApplication with jobApplicationCid=self        
        archive self

template JobContract
  with
    hrManager: Party
    employee: Party
  where
    signatory hrManager, employee

    ensure hrManager /= employee

    key (hrManager, employee): (Party, Party)
    maintainer [key._1, key._2]
